apply plugin: "groovy"

repositories {
	flatDir name: 'localRepository', dirs: 'lib' 
	mavenCentral()
	mavenRepo urls: [ 
			"https://nexus.codehaus.org/content/repositories/snapshots",
			"https://github.com/operasoftware/operadriver/tree/master/maven"
	]
}

configurations {
	testCompile.transitive = true
}

// The drivers we want to use
drivers = ["htmlunit", "firefox", "chrome","ie"]//,"android"]

dependencies {
	groovy "org.codehaus.groovy:groovy-all:1.7.10"
	
	def gebVersion = "0.6.0"
	def seleniumVersion = "latest.release"//"2.0rc3"//"2.3.1"
	
	// If using Spock, need to depend on geb-spock
	testCompile "org.codehaus.geb:geb-spock:$gebVersion"
	testCompile "org.spockframework:spock-core:0.5-groovy-1.7"
	
	// If using JUnit, need to depend on geb-junit (3 or 4)
	testCompile "org.codehaus.geb:geb-junit4:$gebVersion"
	testCompile "junit:junit-dep:4.8.2"
	
	// Drivers
	drivers.each { driver ->
		testCompile "org.seleniumhq.selenium:selenium-$driver-driver:$seleniumVersion"
	}
	testCompile "com.opera:operadriver:0.6"

}

drivers.each { driver ->
	task "${driver}Test"(type: Test) {
		testReportDir = file("$reportsDir/$name/tests")
		testResultsDir = file("$buildDir/test-results/$name")
		
		systemProperty "geb.build.reportsDir", "$reportsDir/$name/geb"
		systemProperty "geb.env", driver
		
		// If you wanted to set the baseUrl in your buildâ€¦
		// systemProperty "geb.build.baseUrl", "http://myapp.com"
	}
}


task test(overwrite: true, dependsOn: drivers.collect { tasks["${it}Test"] })

task wrapper(type: Wrapper) {
	gradleVersion = '1.0-milestone-3'
}

